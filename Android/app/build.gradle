plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId 'com.notnotme.memegl'
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 26
        versionName '49.3'
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField 'Boolean', 'STRICT_MODE', 'false'
    }
    buildTypes {
        debugStrictMode {
            initWith debug
            buildConfigField 'Boolean', 'STRICT_MODE', 'true'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'version'
    productFlavors {
        demo {
            dimension 'version'
            buildConfigField 'Boolean', 'DEMO_MODE', 'true'
        }
        full {
            dimension 'version'
            buildConfigField 'Boolean', 'DEMO_MODE', 'false'
        }
    }
    variantFilter { variant ->
        // full version doesn't need to be tested in strict mode
        def names = variant.flavors*.name
        if (variant.buildType.name == 'debugStrictMode' && names.contains('full')) {
            setIgnore true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        exclude 'DebugProbesKt.bin'
    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.camera:camera-camera2:1.0.2'
    implementation 'androidx.camera:camera-lifecycle:1.0.2'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.mlkit:face-detection:16.1.3'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    debugStrictModeImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    debugImplementation 'androidx.fragment:fragment-testing:1.4.0'

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.navigation:navigation-testing:2.3.5'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    /*
     * Since Android S (31+), tests are broken and some of the below libraries needs to be updated
     * error is
     * "Targeting S+ (version 31 and above) requires that one of FLAG_IMMUTABLE or FLAG_MUTABLE be specified when creating a PendingIntent."
     */
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'androidx.test:core:1.3.0' // broke with 1.4.0
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'

}